# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='cargo-deb'
pkgver="1.42.2"
pkgrel=1
pkgdesc='Cargo subcommand that generates Debian packages'
arch=('x86_64')
url="https://github.com/kornelski/cargo-deb"
license=('MIT')
makedepends=('cargo')
source=("${url}/archive/v${pkgver}.tar.gz")
sha256sums=('0f789a1c66995e1a659f6a11e5ff56b0bee5425cac90c6fd1bef5c39fbc34389')

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
