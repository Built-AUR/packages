# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='cargo-deb'
pkgver="1.42.1"
pkgrel=1
pkgdesc='Cargo subcommand that generates Debian packages'
arch=('x86_64')
url="https://github.com/kornelski/cargo-deb"
license=('MIT')
makedepends=('cargo')
source=("${url}/archive/v${pkgver}.tar.gz")
sha256sums=('88bf42498fa3e3ea6545ddbf652bc22f5ef5016ddf375205da0e8a3184bc371a')

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
