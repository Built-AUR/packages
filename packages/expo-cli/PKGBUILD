# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='expo-cli'
pkgver="6.0.1"
pkgrel=1
pkgdesc='Tools for creating, running, and deploying Universal Expo and React Native apps'
arch=('any')
url="https://www.npmjs.com/package/${pkgname}"
license=('MIT')
depends=('nodejs')
makedepends=('npm')
_file="${pkgname}-${pkgver}.tgz"
source=("https://registry.npmjs.org/${pkgname}/-/${_file}")
noextract=("${_file}")
sha256sums=('bce364245cea8f7d594853ba0dd354107439d2b480d1c0c8da2dadb9fb01af97')

package() {
  npm install -g --prefix "${pkgdir}/usr" "${_file}"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "${pkgdir}"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "${pkgdir}"
}
