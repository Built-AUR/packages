# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='aws-cdk'
pkgver="2.55.1"
pkgrel=1
pkgdesc='CDK Toolkit, the command line tool for CDK app'
arch=('any')
url="https://github.com/awslabs/aws-cdk"
license=('Apache')
depends=('nodejs')
makedepends=('npm')
_file="${pkgname}-${pkgver}.tgz"
source=("https://registry.npmjs.org/${pkgname}/-/${_file}")
noextract=("${_file}")
sha256sums=('d69e052a8c5f9ab39f02b05ea2f2fd3a0f36644aeb5b6e584e532711216f62fd')

package() {
  npm install -g --prefix "${pkgdir}/usr" "${_file}"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "${pkgdir}"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "${pkgdir}"
}
