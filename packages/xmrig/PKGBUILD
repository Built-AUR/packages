# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgbase='xmrig'
pkgname=('xmrig' 'xmrig-donateless')
pkgver="6.18.0"
pkgrel=1
pkgdesc='RandomX, KawPow, CryptoNight, AstroBWT and GhostRider unified CPU/GPU miner and RandomX benchmark '
arch=('x86_64')
url="https://github.com/xmrig/xmrig"
license=('MIT')
depends=('libuv' 'libmicrohttpd' 'openssl' 'hwloc')
makedepends=('cmake' 'sed')
optdepends=('ocl-icd: AMD GPU mining'
            'opencl-headers: AMD GPU mining'
            'cuda: nVidia GPU mining')
source=("${pkgbase}-${pkgver}.tar.gz::${url}/archive/v${pkgver}/${pkgbase}-v${pkgver}.tar.gz"
        "${pkgbase}.service"
        "${pkgbase}.sysusers")
sha256sums=('4531a31c0c095fcae18fdef0157f1e2a6694408abbcff6789c8f3cd6ab2c3ca0'
            'b4c7a9fb3084f15f091b6d85ca641470c5056da622c064b61e0708f2d886841e'
            'd8f499302fb2b642fe02586c81c410a299e0a6e133aef1cc1c783bcdcb3f44f6')

prepare() {
  mkdir "${pkgbase}-${pkgver}/build-xmrig"
  mkdir "${pkgbase}-${pkgver}/build-xmrig-donateless"
}

build() {
  echo '==> Building xmrig...'
  cd "${srcdir}/${pkgbase}-${pkgver}/build-xmrig"
  cmake -DCMAKE_BUILD_TYPE=Release ..
  cmake --build .

  echo '==> Building xmrig-donateless...'
  cd "${srcdir}/${pkgbase}-${pkgver}"

  sed -i 's/constexpr const int kDefaultDonateLevel = 1;/constexpr const int kDefaultDonateLevel = 0;/g' src/donate.h
  sed -i 's/constexpr const int kMinimumDonateLevel = 1;/constexpr const int kMinimumDonateLevel = 0;/g' src/donate.h

  cd "${srcdir}/${pkgbase}-${pkgver}/build-xmrig-donateless"
  cmake -DCMAKE_BUILD_TYPE=Release ..
  cmake --build .
}

package_xmrig() {
  cd "${pkgbase}-${pkgver}"
  install -Dm 775 "build-xmrig/${pkgbase}" "${pkgdir}/usr/bin/${pkgbase}"
  install -Dm 644 "src/config.json" "${pkgdir}/etc/${pkgbase}/xmrig.conf"
  install -Dm 644 "${srcdir}/${pkgbase}.service" "${pkgdir}/usr/lib/systemd/system/${pkgbase}@.service"
  install -Dm 0644 "${srcdir}/${pkgbase}.sysusers" "${pkgdir}/usr/lib/sysusers.d/${pkgbase}.conf"
}

package_xmrig-donateless() {
  provides=("xmrig=${pkgver}")
  conflicts=('xmrig')
  pkgdesc+=" - No Donate Version"

  cd "${pkgbase}-${pkgver}"
  install -Dm 775 "build-xmrig-donateless/${pkgbase}" "${pkgdir}/usr/bin/${pkgbase}"
  install -Dm 644 "src/config.json" "${pkgdir}/etc/${pkgbase}/xmrig.conf"
  install -Dm 644 "${srcdir}/${pkgbase}.service" "${pkgdir}/usr/lib/systemd/system/${pkgbase}@.service"
  install -Dm 0644 "${srcdir}/${pkgbase}.sysusers" "${pkgdir}/usr/lib/sysusers.d/${pkgbase}.conf"
}
