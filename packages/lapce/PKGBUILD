# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='lapce'
pkgver="0.2.1"
pkgrel=1
pkgdesc='Lightning-fast and Powerful Code Editor written in Rust'
arch=('x86_64')
url="https://github.com/lapce/lapce"
license=('Apache')
depends=('gcc-libs' 'libxcb' 'expat' 'freetype2' 'libxkbcommon' 'fontconfig' 'gtk3')
makedepends=('rust' 'python' 'cmake')
source=("${url}/archive/v${pkgver}.tar.gz")
sha256sums=('af73cf04318a70d02d3aaed21b4e601648f9d777f30f0af4266354782b888a28')
options=('!lto')

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --locked --profile release-lto
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 target/release-lto/${pkgname} "${pkgdir}/usr/bin/${pkgname}"

  # extra desktop stuff
  install -Dm 644 -t "${pkgdir}/usr/share/applications" extra/linux/*.desktop
  install -Dm 644 -t "${pkgdir}/usr/share/metainfo" extra/linux/*.xml
  install -Dm 644 extra/images/logo.png "${pkgdir}/usr/share/pixmaps/dev.lapce.lapce.png"

  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
