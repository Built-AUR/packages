# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='lapce'
pkgver="0.1.3"
pkgrel=1
pkgdesc='Lightning-fast and Powerful Code Editor written in Rust'
arch=('x86_64')
url="https://github.com/lapce/lapce"
license=('Apache')
depends=('gcc-libs' 'libxcb' 'expat' 'freetype2' 'libxkbcommon' 'fontconfig' 'gtk3')
makedepends=('rust' 'python' 'cmake')
source=("${url}/archive/v${pkgver}.tar.gz")
sha256sums=('381f95be6ed43396ecff5da5ce7d2bc75969ba1f67e251410d72753e1059d74d')
options=('!lto')

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --locked --profile release-lto
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 target/release-lto/${pkgname} "${pkgdir}/usr/bin/${pkgname}"

  # extra desktop stuff
  install -Dm 644 -t "${pkgdir}/usr/share/applications" extra/linux/*.desktop
  install -Dm 644 -t "${pkgdir}/usr/share/metainfo" extra/linux/*.xml
  install -Dm 644 extra/images/logo.png "${pkgdir}/usr/share/pixmaps/dev.lapce.lapce.png"

  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
