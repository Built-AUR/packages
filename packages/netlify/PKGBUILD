# Maintainer: MedzikUser <nivua1fn@duck.com>
_pkgname='netlify-cli'
pkgname='netlify'
provides=("${pkgname}-cli")
conflicts=("${pkgname}-cli")
pkgver="12.0.5"
pkgrel=1
pkgdesc='Create, deploy, and delete new sites hosted on Netlify straight from your terminal'
arch=('any')
url="https://www.netlify.com/docs/cli/"
license=('MIT')
depends=('nodejs')
makedepends=('npm')
_file="${_pkgname}-${pkgver}.tgz"
source=("https://registry.npmjs.org/${_pkgname}/-/${_file}")
noextract=("$_file")
sha256sums=('2efcba20ed700d7fe3d6d5d31e0e56b86df01aab86833594c100aac10e80ab40')

package() {
  npm install -g --prefix "${pkgdir}/usr" "${_file}"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "${pkgdir}"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "${pkgdir}"
}
