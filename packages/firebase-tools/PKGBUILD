# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='firebase-tools'
pkgver="12.4.5"
pkgrel=1
pkgdesc='The Firebase Command Line Tools'
arch=('any')
url="https://www.npmjs.com/package/${pkgname}"
license=('MIT')
depends=('nodejs')
makedepends=('npm')
_file="${pkgname}-${pkgver}.tgz"
source=("https://registry.npmjs.org/${pkgname}/-/${_file}")
noextract=("${_file}")
sha256sums=('8ea7ad365d268cf3127c4b1a143333d917674a8a6e4203774c630792e7412850')

package() {
  npm install -g --prefix "${pkgdir}/usr" "${_file}"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "${pkgdir}"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "${pkgdir}"
}
