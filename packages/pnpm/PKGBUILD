# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='pnpm'
pkgver="8.1.0"
pkgrel=1
pkgdesc='Fast, disk space efficient package manager'
arch=('any')
url="https://www.npmjs.com/package/${pkgname}"
license=('MIT')
depends=('nodejs')
makedepends=('npm')
_file="${pkgname}-${pkgver}.tgz"
source=("https://registry.npmjs.org/${pkgname}/-/${_file}")
noextract=("$_file")
sha256sums=('ccfd7f19bc0ff34eaa257e5e2c960d357f6f7ed64952ff634828672a7eb059ee')

package() {
  npm install -g --prefix "${pkgdir}/usr" "${_file}"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "${pkgdir}"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "${pkgdir}"
}
